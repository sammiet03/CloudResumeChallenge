## Importing an S3 Bucket into Terraform

### Overview
This guide explains how to import an existing S3 bucket and its associated configurations (bucket policy and CORS) into Terraform.


### Steps to Import S3 Resources

### 1. Create `s3.tf`

Define the S3 bucket and its related configurations:

```
resource "aws_s3_bucket" "static_site" {
  bucket = "bucketname.com"

  tags = {
    Name      = "bucketname-static-site"
    ManagedBy = "Terraform"
  }
}

resource "aws_s3_bucket_policy" "static_site_policy" {
  bucket = aws_s3_bucket.static_site.id

  policy = jsonencode({
    Version   = "2012-10-17",
    Statement = [
      {
        Sid       = "PublicReadGetObject",
        Effect    = "Allow",
        Principal = "*",
        Action    = "s3:GetObject",
        Resource  = "arn:aws:s3:::bucketname.com/*"
      },
      {
        Sid       = "AllowCloudFrontAccess",
        Effect    = "Allow",
        Principal = {
          AWS = "arn:aws:iam::cloudfront:user/CloudFront Origin Access Identity YOUR_OAI_ID"
        },
        Action    = "s3:GetObject",
        Resource  = "arn:aws:s3:::bucketname.com/*"
      }
    ]
  })
}

resource "aws_s3_bucket_cors_configuration" "static_site_cors" {
  bucket = aws_s3_bucket.static_site.id

  cors_rule {
    allowed_methods = ["GET", "HEAD"]
    allowed_origins = ["*"]
    allowed_headers = ["*"]
    expose_headers  = ["ETag"]
    max_age_seconds = 3000
  }
}
```

> **Note:** Replace `bucketname.com` and `YOUR_OAI_ID` with your actual bucket name and CloudFront Origin Access Identity.


### 2. Initialize Terraform

```
terraform init
```


### 3. Import the S3 Bucket

```
terraform import aws_s3_bucket.static_site bucketname
```


### 4. Import the Bucket Policy

First, retrieve the existing bucket policy:

```
aws s3api get-bucket-policy --bucket bucketname
```

Then import the policy into Terraform:

```
terraform import aws_s3_bucket_policy.static_site_policy bucketname
```



### 5. Import the CORS Configuration

```
terraform import aws_s3_bucket_cors_configuration.static_site_cors bucketname
```


### 6. Finalize and Validate

Run the following commands to review and apply changes:

```
terraform plan
terraform apply
```


